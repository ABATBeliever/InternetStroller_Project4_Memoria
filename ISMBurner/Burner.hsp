#packopt version "def.ini"
#packopt icon "image.ico"
#uselib "shell32.dll"
#cfunc SHBrowseForFolder "SHBrowseForFolder" int
#func SHGetPathFromIDList "SHGetPathFromIDList" int,int
#uselib "ole32.dll"
#func CoTaskMemFree "CoTaskMemFree" int
#uselib "crtdll"
#func rename "rename" str, str
#pack "helptxt.pak"
#pack "rdtxt.pak"
#pack "ISMexe.pak"
#pack "cfini.pak"
#define CLSID_ShellLink   "{00021401-0000-0000-C000-000000000046}"
#define IID_IShellLinkA   "{000214EE-0000-0000-C000-000000000046}"
#define IID_IPersistFile  "{0000010b-0000-0000-C000-000000000046}"
#usecom IShellLinkA IID_IShellLinkA
#comfunc IShellLink_SetPath 20 str
#usecom IPersistFile IID_IPersistFile
#comfunc IPersistFile_Save 6 wstr,int
font "HG行書体", 9, 0
product="InternetStroller"
langage="Japanese"
thisver="1.4.0.0"
inspath="ﾃﾞｨﾚｸﾄﾘを指定"
onexit *inputstop
*startsite
title product+" Burner "+thisver
screen 0,350,330
cls 1
mes product+" Burner "+thisver
mes "<===============================>"
mes "このバーナーを使用して、InternetStrollerを"
mes "USBに焼いたり、ｲﾝｽﾄｰﾙすることができます"
mes "ﾚｼﾞｽﾄﾘ等には触りません。"
mes ""
mes "統合されたﾊﾞｰｼﾞｮﾝ:1.4.0.0"
mes ""
pos 0,300:button "ｷｬﾝｾﾙ", *inputstop
pos 70,300:button "戻る", *startsite
pos 140,300:button "続行", *installcheck
stop

*installcheck
*installlicense
screen 0,350,330
cls 1
license="InternetStroller Memoria 本体とﾊﾞｰﾅｰのﾗｲｾﾝｽ\n\n本体\n"
license=license+"Copyright (C) 2021-2023 ABATBeliever. All rights reserved.\n1:InternetStrollerMemoriaはABATBelieverの個人開発品です。\n2:このｿﾌﾄで発生したいかなる問題についても、ABATBelieverは責任を免責されるものとします。\n3:当該ﾊﾞｰｼﾞｮﾝのISがｲﾝｽﾄｰﾙされます。もしもISのﾗｲｾﾝｽが変更されたとしても、このﾗｲｾﾝｽのみがこのﾊﾞｰｼﾞｮﾝでは有効です\n4: 'WebView2'ｴﾝｼﾞﾝが使用されています。 そのﾗｲｾﾝｽは下部に記載されています\n5:'mod_WebView2_10115038.hsp'が含まれていますが、これはgooから提供されました。"
license=license+"\n\nﾊﾞｰﾅｰ\nこのｱﾌﾟﾘはInternetStrollerをUSB等にｲﾝｽﾄｰﾙするための付属ﾂｰﾙです。自己責任でお願いします。"
license=license+"\n\nWebView2Loader2.dll\nCopyright (C) Microsoft Corporation. All rights reserved.\n\nRedistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are\nmet:\n* Redistributions of source code must retain the above copyright\nnotice, this list of conditions and the following disclaimer.\n* Redistributions in binary form must reproduce the above"
license=license+"\ncopyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.\n* The name of Microsoft Corporation, or the names of its contributors "
license=license+"may not be used to endorse or promote products derived from this software without specific prior written permission.\n\n"
license=license+"THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS\n'AS IS' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT\nLIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR\nA PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT\nOWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,"
license=license+"\nSPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT\nLIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,\nDATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY\nTHEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT"
license=license+"(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE\nOF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE."
mes product+" Burner "+thisver
mes "<===============================>"
mes "ﾗｲｾﾝｽ"
mesbox license, 350, 205, 0, 0
objsel 0
mes ""
pos 0,265:chkbox "同意", agreecheck
pos 0,300:button "ｷｬﾝｾﾙ", *inputstop
pos 70,300:button "戻る", *startsite
pos 140,300:button "続行", *installcheck2
stop

*installcheck2
if agreecheck = "1" {goto *installversion}
dialog "同意しない場合ｾｯﾄｱｯﾌﾟ出来ません"
stop

*installversion
onexit *inputstop
screen 0,350,330
cls 1
mes product+" Burner "+thisver
mes "<===============================>"
mes "ﾊﾞｰｼﾞｮﾝとﾃﾞｨﾚｸﾄﾘ"
mes ""
mes "ﾊﾞｰｼﾞｮﾝを選択"
listbox installver, 20, "選択\nx86"
mes "ｲﾝｽﾄｰﾙ先"
sdim inspath, 1024
inspath=dir_desktop+"\\ISMemoria"
input inspath,282, 20
pos 0,180:mes "ｲﾝｽﾄｰﾙするファイル"
pos 283,150:button ",,,", *openpath
pos 0,300:button "ｷｬﾝｾﾙ", *inputstop
pos 70,300:button "戻る", *installlicense
pos 140,300:button "続行", *installversion2
objsize 150, 24
pos 0,200:chkbox "readme.txt(推奨)", readmeins
pos 0,220:chkbox "help.txt(推奨)", helpins
pos 0,240:chkbox "実行ﾌｧｲﾙ(推奨)", jins
pos 0,260:chkbox "英語ﾌﾟﾗｸﾞｲﾝ(ｵﾌﾟｼｮﾝ)", eins
pos 180,180:mes "ｲﾝｽﾄｰﾙｵﾌﾟｼｮﾝ"
objsize 150, 24
pos 180,200:chkbox "ﾃﾞｽｸﾄｯﾌﾟにｼｮｰﾄｶｯﾄを作成", shortcut
stop

*installversion2
if installver = "1" {goto *installq}
dialog "ﾊﾟｯｹｰｼﾞが無効か、存在しません"
stop

*openpath
dim ret
dim prm,16
sdim SubTitle,  260
sdim FolderName,260
SubTitle="焼くﾌｫﾙﾀﾞを選択してください"
prm(0)=hwnd,0,varptr(FolderName),varptr(SubTitle)
prm(4)=0x01,0,0,0
ret=SHBrowseForFolder(varptr(prm))
if ret==0:return 0
SHGetPathFromIDList ret,varptr(inspath)
CoTaskMemFree ret
inspath=inspath+"\\ISMemoria"
objprm 1, inspath
dirlist x, inspath, 5
if stat = 1 : dialog "ﾌｫﾙﾀﾞが既に存在します、書き込んだ場合元ﾌｫﾙﾀﾞはﾘﾈｰﾑされます"
stop

*installq
screen 0,350,330
cls 1
mes product+" Burner "+thisver
mes "<===============================>"
usefilename=""
if readmeins = "1" {usefilename=usefilename+"readme.txt\n"}
if helpins = "1" {usefilename=usefilename+"help.txt\n"}
if jins = "1" {usefilename=usefilename+"ISMemoria.exe\n"}
if eins = "1" {usefilename=usefilename+"conf.ini"}
instext="以下の通り書き込まれます。よろしいですか?\nﾊﾞｰｼﾞｮﾝ:x86 1.3.2\nﾃﾞｨﾚｸﾄﾘ"+inspath
instext=instext+"\n\n対象ﾌｧｲﾙ\n"+usefilename+"\n\nｵﾌﾟｼｮﾝ\n"
if shortcut = "1" {instext=instext+"ﾃﾞｽｸﾄｯﾌﾟにｼｮｰﾄｶｯﾄを作成\n("+dir_desktop+")"}
mesbox instext, 300, 250, 0, 0
pos 0,300:button "ｷｬﾝｾﾙ", *inputstop
pos 70,300:button "戻る", *installversion
pos 140,300:button "続行", *install
stop

*install
onexit *stopinstall
screen 0,350,330
cls 1
mes product+" Burner "+thisver
mes "<===============================>"
instext="Write Start\nin "+inspath+"\n<"+gettime(1)+"/"+gettime(3)+" "+gettime(4)+":"+gettime(5)+":"+gettime(6)+">"
mesbox instext, 300, 250, 0, 0
//フォルダが存在するか
instext=instext+"\nﾌｫﾙﾀﾞ確認"
objprm 0, instext
dirlist x, inspath, 5
if stat = 1 {
tmp=inspath+"_old"+gettime(1)+gettime(3)+gettime(4)+gettime(5)+gettime(6)
rename inspath, tmp
}
mkdir inspath
await 100
instext=instext+" [完了]"
objprm 0, instext
await 100
//コピー作業
if readmeins = "1" {
instext=instext+"\nhelp.txtを展開"
objprm 0, instext
bcopy "helptxt.pak",""+inspath+"\\help.txt"
await 100
}

if helpins = "1" {
instext=instext+"\nreadme.txtを展開"
objprm 0, instext
bcopy "rdtxt.pak",""+inspath+"\\readme.txt"
await 100
}

if jins = "1" {
instext=instext+"\n実行ﾌｧｲﾙ(ISMemoria.exe)を展開"
objprm 0, instext
bcopy "ISMexe.pak",""+inspath+"\\ISMemoria.exe"
}

if eins = "1" {
instext=instext+"\n英語ﾌﾟﾗｸﾞｲﾝ(conf.ini)を展開"
objprm 0, instext
bcopy "cfini.pak",""+inspath+"\\conf.ini"
await 100
}

if shortcut = "1" {
	if jins = "1"{
		instext=instext+"\nｼｮｰﾄｶｯﾄを作成中"
		objprm 0, instext
		newcom slink, CLSID_ShellLink
		IShellLink_SetPath slink, inspath+"\\ISMemoria.exe"
		IPersistFile_Save  slink, dir_desktop+"\\ISMemoria.lnk", 1
		delcom slink
	}
}

onexit *stopprogram
objprm 0, instext+"\n\n完了"

pos 70,300:button "もういちど", *installversion
pos 140,300:button "終了", *stopprogram
stop


*stopinstall
onexit *inputstop
cls 1
screen 0,350,330
mes product+" Burner "+thisver
mes "<===============================>"
dialog "何らかの原因により失敗しました"
goto *startsite

*inputstop
dialog "ｲﾝｽﾄｰﾙを中断しますか?ｲﾝｽﾄｰﾙ中の場合を除き、保存されません",2
if stat = "6" {goto *stopprogram}
stop

*stopprogram
end